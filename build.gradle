buildscript {
	repositories {
		jcenter()
		maven {
			name 'forge'
			url 'http://files.minecraftforge.net/maven/'
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
		maven {
			name = 'sponge'
			url = 'http://repo.spongepowered.org/maven'
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
		classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
		classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle.liteloader'
apply plugin: 'org.spongepowered.mixin'

sourceCompatibility = 1.6
targetCompatibility = 1.6
sourceSets {
	main {
		java {
			srcDir "lib/MC-Commons/mc-src"
			srcDir "pf-src"
			srcDir "minelp_dummy"
		}
		resources {
			srcDirs "pf-resources"
		}
		refMap = "mixins.presencefootsteps.refmap.json"
	}
}

// Basic project information
version = "u11"
group = "eu.ha3"
archivesBaseName = 'presencefootsteps'
ext.revision = "0"

minecraft {
	version = project.mcVersion
	mappings = project.mcMappings
	runDir = 'jars'
}

mixin {
	defaultObfuscationEnv notch
}

processResources{
	// this will ensure that this task is redone when the versions change.
	def props = [
		version: version,
		mcversion: minecraft.version,
		revision: revision
	]
	inputs.properties props
	// replace stuff in litemod.json, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'litemod.json'
		// replace version and mcversion
		expand props
	}
	
	// copy everything else, thats not the litemod.json
	from(sourceSets.main.resources.srcDirs) {
		exclude 'litemod.json'
	}
}